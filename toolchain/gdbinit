set disassembly-flavor intel

python
import os
gdb.execute('file ' + os.environ['BUILDROOT'] + 'sysroot/boot/kernel')
end

target remote:1234

set height 0
set width 0

define q
monitor quit
end

define reset
monitor system_reset
end

define mmap
monitor info mem
end

define V2P
	if $argc == 1
		print/x $arg0 - 0xFFFFFF8000000000
	end
end

define P2V
	if $argc == 1
		print/x $arg0 + 0xFFFFFF8000000000
	end
end

define restore_env
set $name = $arg0
python

registers = {r: gdb.parse_and_eval('$name->' + r) for r in
['rax', 'rbx', 'rcx', 'rdx', 'rsi', 'rdi', 'rbp', 'rsp', 'r8', 'r9', 'r10',
'r11', 'r12', 'r13', 'r14', 'r15', 'rip']}

for r in registers.items():
  gdb.parse_and_eval('$%s=%s' % r)
gdb.execute('frame 0')
end
end
